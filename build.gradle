plugins {
	id "com.github.kt3k.coveralls"	version "2.8.2";
}


apply plugin: "wrapper"
apply plugin: "java-library"
apply plugin: "jacoco"
apply plugin: "maven-publish"


sourceCompatibility = 10
targetCompatibility = 10


repositories {
	mavenCentral();
}


dependencies {
	implementation		group: "com.fasterxml.jackson.core",	name: "jackson-databind",		version: "2.9.4";
	implementation		group: "org.junit.jupiter", 			name: "junit-jupiter-api", 		version: "5.1.0";

	testCompileOnly 	group: "org.junit.jupiter", 			name: "junit-jupiter-api", 		version: "5.1.0";
	
	testRuntimeOnly 	group: "org.junit.jupiter", 			name: "junit-jupiter-engine", 	version: "5.1.0";
}


compileJava {
	options.encoding = "UTF-8";
}


wrapper {
	gradleVersion = "4.9";
}


test {
	// Enable JUnit 5 Platform support
	useJUnitPlatform();
	
	testLogging {
		events = [ "skipped", "failed" ];
		exceptionFormat = "full";
	}
}


jacoco {
	toolVersion = "0.8.1";
}


jacocoTestReport {
	reports {
		xml.enabled = true;
		xml.destination = file( "$buildDir/reports/jacoco/xml/jacoco_test_report.xml" );
		html.enabled = true;
		html.destination = file( "$buildDir/reports/jacoco/html" );
	}
}


coveralls {
	jacocoReportPath = "$buildDir/reports/jacoco/xml/jacoco_test_report.xml";
}


task sourcesJar( type: Jar, dependsOn: classes ) {
	from sourceSets.main.allJava;
	classifier "sources";
}


task javadocJar( type: Jar, dependsOn: javadoc ) {
	from javadoc;
	classifier = "javadoc";
}


artifacts {
	archives sourcesJar;
	archives javadocJar;
}


publishing {
	publications {
		BintrayPublication( MavenPublication ) {
			from components.java;
			
			groupId = "pt.pcaleia";
			artifactId = "utils";
			version = System.getenv( "TRAVIS_TAG" );
			
			artifact sourcesJar;
			artifact javadocJar;
		}
	}
}
